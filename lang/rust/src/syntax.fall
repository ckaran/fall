tokenizer {
  pub 'pub'
  let 'let'
  struct 'struct'
  fn 'fn'
  use 'use'

  lparen '('
  rparen ')'
  lbrace '{'
  rbrace '}'
  eq '='
  semi ';'
  colon ':'
  coloncolon '::'
  comma ','

  whitespace r"\s+"
  ident r"\p{XID_Start}\w*"
  number r"\d+"
}

pub rule file {
  <rep <with_skip file_form_first file_form>>
}

rule file_form_first { 
  'pub' | 'fn' | 'struct' | 'use'
}

rule file_form { 
  use_decl | fn_def | struct_def
}

pub rule use_decl {
  'use' <commit> path ';'
}

pub rule fn_def {
  <opt visibility> 'fn' <commit> ident
  '(' ')' block_expr
}


pub rule struct_def {
  <opt visibility> 'struct' <commit> ident {
    '{' <layer in_braces <rep struct_field>> '}'
  | ';'
  | '(' <layer in_parens <rep tuple_field>> ')' ';'
  }
}

pub rule struct_field {
  <opt visibility> ident <commit> ':' type { <eof> | ',' }
}

pub rule tuple_field {
  <opt visibility> type <commit> { <eof> | ',' }
}

pub rule visibility {
  'pub'
}


#[pratt]
pub rule path {
  unqualified_path | qualified_path
}

#[atom, type(path)]
pub rule unqualified_path {
  <opt '::'> ident
}

#[postfix, type(path)]
pub rule qualified_path {
  path {'::' ident}
}


pub rule type {
  ident
}


pub rule block_expr {
  '{' <layer in_braces <rep stmt>> '}'
}

rule in_braces { <rep brace_balanced> }
rule brace_balanced {
  '{' <commit> in_braces '}'
| <not '}'>
}

rule in_parens { <rep paren_balanced> }
rule paren_balanced {
  '(' <commit> in_parens ')'
| <not ')'>
}


pub rule stmt {
  'let' <commit> pattern '=' expr ';'
}

pub rule pattern { ident }
pub rule expr { number }
